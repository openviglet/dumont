@echo off
rem --------------------------------------------------------------------------
rem package-dist.bat
rem Builds Dumont from source and creates distribution packages:
rem   - dumont-install.zip   (portable, works on Linux and Windows)
rem   - dumont-install.exe   (Windows installer via NSIS, if available)
rem
rem Usage:  scripts\package-dist.bat [--skip-build]
rem --------------------------------------------------------------------------
setlocal enabledelayedexpansion

set SCRIPT_DIR=%~dp0
set PROJECT_ROOT=%SCRIPT_DIR%..
set SKIP_BUILD=false
set "PS=%SystemRoot%\System32\WindowsPowerShell\v1.0\powershell.exe"

:parse_args
if "%~1"=="" goto end_args
if /i "%~1"=="--skip-build" (
    set SKIP_BUILD=true
    shift
    goto parse_args
)
echo Unknown option: %~1
exit /b 1
:end_args

pushd "%PROJECT_ROOT%"

rem --------------- Build ---------------
if "%SKIP_BUILD%"=="true" goto :skip_build
echo ==^> Building Dumont...
where mvn >nul 2>&1
if %errorlevel%==0 (
    call mvn clean install -DskipTests -Dgpg.skip=true -Dturing.open-browser=false
) else (
    call mvnw.cmd clean install -DskipTests -Dgpg.skip=true -Dturing.open-browser=false
)
if errorlevel 1 (
    echo Build failed.
    popd
    exit /b 1
)
:skip_build

rem =====================================================================
rem  Shared: copy artifacts into a staging area
rem =====================================================================
echo ==^> Staging artifacts...

set DIST_NAME=dumont-install
set STAGE=%PROJECT_ROOT%\target\dist-stage\%DIST_NAME%
if exist "%STAGE%" rmdir /s /q "%STAGE%"
mkdir "%STAGE%\connector\libs\aem"
mkdir "%STAGE%\connector\libs\webcrawler"
mkdir "%STAGE%\connector\export"
mkdir "%STAGE%\db"
mkdir "%STAGE%\filesystem"
mkdir "%STAGE%\bin"

rem Connector engine
copy "connector\connector-app\target\dumont-connector.jar" "%STAGE%\connector\" >nul

rem Connector plugins
copy "aem\aem-plugin\target\aem-plugin.jar"               "%STAGE%\connector\libs\aem\" >nul
copy "web-crawler\wc-plugin\target\web-crawler-plugin.jar" "%STAGE%\connector\libs\webcrawler\" >nul

rem AEM Sample plugin + export files
copy "aem\aem-plugin-sample\target\aem-plugin-sample.jar"  "%STAGE%\connector\libs\aem\" >nul
copy "aem\aem-plugin-sample\scripts\wknd\export\wknd.json"  "%STAGE%\connector\export\" >nul
copy "aem\aem-plugin-sample\scripts\wknd\export\wknd2.json" "%STAGE%\connector\export\" >nul

rem Standalone CLI tools
copy "db\db-app\target\dumont-db.jar"                      "%STAGE%\db\" >nul
copy "filesystem\fs-connector\target\dumont-filesystem.jar" "%STAGE%\filesystem\" >nul

rem Scripts
copy "%SCRIPT_DIR%bin\dumont-aem.sh"         "%STAGE%\bin\" >nul
copy "%SCRIPT_DIR%bin\dumont-aem.bat"        "%STAGE%\bin\" >nul
copy "%SCRIPT_DIR%bin\dumont-webcrawler.sh"  "%STAGE%\bin\" >nul
copy "%SCRIPT_DIR%bin\dumont-webcrawler.bat" "%STAGE%\bin\" >nul
copy "%SCRIPT_DIR%bin\dumont-db.sh"          "%STAGE%\bin\" >nul
copy "%SCRIPT_DIR%bin\dumont-db.bat"         "%STAGE%\bin\" >nul
copy "%SCRIPT_DIR%bin\dumont-filesystem.sh"  "%STAGE%\bin\" >nul
copy "%SCRIPT_DIR%bin\dumont-filesystem.bat" "%STAGE%\bin\" >nul

rem Config and docs
copy "%SCRIPT_DIR%config\dumont-connector.properties" "%STAGE%\connector\" >nul
copy "%SCRIPT_DIR%config\README.txt" "%STAGE%\" >nul

rem =====================================================================
rem  1. Zip
rem =====================================================================
set ZIP_OUTPUT=%PROJECT_ROOT%\target\%DIST_NAME%.zip
echo ==^> Creating zip: %ZIP_OUTPUT%
if exist "%ZIP_OUTPUT%" del "%ZIP_OUTPUT%"

set STAGE_PARENT=%PROJECT_ROOT%\target\dist-stage
pushd "%STAGE_PARENT%"
"!PS!" -NoProfile -Command "Compress-Archive -Path '%DIST_NAME%' -DestinationPath '%ZIP_OUTPUT%'"
popd

rem =====================================================================
rem  2. Windows Installer (NSIS)
rem =====================================================================
set MAKENSIS=

where makensis >nul 2>&1
if %errorlevel%==0 (
    set MAKENSIS=makensis
    goto :build_nsis
)

if exist "C:\Program Files (x86)\NSIS\makensis.exe" (
    set "MAKENSIS=C:\Program Files (x86)\NSIS\makensis.exe"
    goto :build_nsis
)

if exist "C:\Program Files\NSIS\makensis.exe" (
    set "MAKENSIS=C:\Program Files\NSIS\makensis.exe"
    goto :build_nsis
)

echo ==^> Skipping Windows installer (NSIS not found)
goto :done

:build_nsis
echo ==^> Creating Windows installer...
if not exist "%PROJECT_ROOT%\target" mkdir "%PROJECT_ROOT%\target"
"%MAKENSIS%" /V2 /DOUTDIR="%PROJECT_ROOT%\target" /DSTAGE="%STAGE%" "%SCRIPT_DIR%installer\dumont.nsi"
if errorlevel 1 (
    echo Installer build failed.
) else (
    echo    target\%DIST_NAME%.exe
)

:done
echo.
echo ==^> All done. Outputs in target\

popd
endlocal
